scn LootTrackerAddTarget;              Script Type: Object

;=========================================================================
; Copyright 2023 Roman Kuchma
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;=========================================================================

;=========================================================================
; Adds a new or loaded objectivetarget. Starts the quest if necessary.
;
; Param rTarget
;	New or existing actor to add as an objective target
;
; Called from:
; - LootTrackerHandleDeath
; - LootTrackerHandlePostLoadGame
;=========================================================================

ref rTarget
Begin Function { rTarget }
	PrintDebug "LootTrackerAddTarget " + $rTarget

	; Check if the target was not saved already

	if eval (Ar_Find rTarget LootTracker.aObjectiveTargets) == Ar_BadNumericIndex

		; Target is not saved yet - the script is called from
		; LootTrackerHandleDeath.

		; Check if the target is configured to be tracked and add it.

		if Call LootTracker.CheckCanTrackSpecies rTarget

			if eval (Ar_Size LootTracker.aObjectiveTargets) < 1

				; Start the quest if this is the first target
	
				Call LootTracker.SetInputHandlers
				Call LootTracker.StartTheQuest
			endif
		
			; Add the passed actor (pun intended) to the objective targets
			Ar_Append LootTracker.aObjectiveTargets rTarget
			AddObjectiveTarget LootTracker 10 rTarget
			Call LootTracker.TryActivateQuest
		else
			PrintDebug $rTarget + " " + (GetFormIDString rTarget) + " filtered out"
		endif

	else

		; The target is already saved - the script was called from
		; LootTrackerHandlePostLoadGame

		PrintDebug $rTarget + " " + (GetFormIDString rTarget) + " is already saved"

		Call LootTracker.SetInputHandlers

		; Add the passed actor (pun intended) to the objective targets

		AddObjectiveTarget LootTracker 10 rTarget
	endif

End